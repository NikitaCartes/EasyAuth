plugins {
	id "com.github.johnrengelman.shadow" version "5.0.0"
}

repositories {
	// Carpet mod
	maven {
		url 'https://jitpack.io'
	}
	maven {
		url 'https://masa.dy.fi/maven'
	}
}

configurations {
	shadow
}

architectury {
	platformSetupLoomIde()
}

dependencies {
	modImplementation("net.fabricmc:fabric-loader:${rootProject.loader_version}")
	modImplementation("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}")

	compileOnly(project(path: ":common")) {
		transitive = false
	}
	runtimeOnly(project(path: ":common", configuration: "transformDevelopmentFabric")) {
		transitive = false
	}
	shadow(project(path: ":common", configuration: "transformProductionFabric")) {
		transitive = false
	}


	// carpetMod
	// from masa's maven
	//modImplementation "carpet:fabric-carpet:${project.minecraft_version}-${project.carpet_core_version}"
	// jitpack for quicker updating
	modImplementation "com.github.gnembon:fabric-carpet:${project.carpet_branch}-SNAPSHOT"

	// Password hashing
	// Argon2
	implementation "de.mkammerer:argon2-jvm:${argon2_version}"
	include "de.mkammerer:argon2-jvm:${argon2_version}"

	// BCrypt
	implementation "at.favre.lib:bcrypt:${bcrypt_version}"
	implementation "at.favre.lib:bytes:${bytes_version}"
	include "at.favre.lib:bcrypt:${bcrypt_version}"
	include "at.favre.lib:bytes:${bytes_version}"

	// Storage
	// LevelDB database
	implementation 'org.iq80.leveldb:leveldb:0.12'
	implementation 'org.iq80.leveldb:leveldb-api:0.12'
	include 'org.iq80.leveldb:leveldb:0.12'
	include 'org.iq80.leveldb:leveldb-api:0.12'

	// MongoDB driver
	implementation 'org.mongodb:mongodb-driver-sync:4.1.0'
	include 'org.mongodb:mongodb-driver-sync:4.1.0'

	// JNA lib
	include 'net.java.dev.jna:jna:5.5.0'
}

processResources {
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
	inputs.property "version", project.version
}

shadowJar {
	configurations = [project.configurations.shadow]
	classifier "shadow"
}

remapJar {
	dependsOn(shadowJar)
	input.set(shadowJar.archivePath)
	classifier "fabric"
}
